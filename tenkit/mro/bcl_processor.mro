#
# Copyright (c) 2014 10X Genomics, Inc. All rights reserved.
#
@include "_bcl_processor_stages.mro"

pipeline BCL_PROCESSOR(
    in  path   run_path             "Illumina output folder",
    in  bool   allow_no_barcodes,
    out path   fastq_path,
    out json   demultiplex_summary,
    out string si_read_type,
)
{
    call local preflight BCL_PROCESSOR_PREFLIGHT_LOCAL(
        run_path          = self.run_path,
        allow_no_barcodes = self.allow_no_barcodes,
        check_executables = false,
    )

    call preflight BCL_PROCESSOR_PREFLIGHT(
        run_path          = self.run_path,
        allow_no_barcodes = self.allow_no_barcodes,
        check_executables = true,
    )

    call COMPUTE_DEMUX_PARAMS(
        run_path = self.run_path,
    )

    call volatile BARCODE_AWARE_BCL2FASTQ(
        run_path      = self.run_path,
        num_threads   = 6,
        split_by_tile = COMPUTE_DEMUX_PARAMS.split_by_tile,
    )

    call volatile DEMULTIPLEX(
        raw_fastq_path          = BARCODE_AWARE_BCL2FASTQ.raw_fastq_path,
        rc_i2_read              = COMPUTE_DEMUX_PARAMS.rc_i2_read,
        si_read_type            = COMPUTE_DEMUX_PARAMS.si_read_type,
        sample_index_error_rate = 0.15,
        interleave              = true,
        split_by_tile           = COMPUTE_DEMUX_PARAMS.split_by_tile,
    )

    call MERGE_FASTQS_FROM_TILES(
        demultiplexed_fastq_path = DEMULTIPLEX.demultiplexed_fastq_path,
        common_bcs               = DEMULTIPLEX.common_bcs,
        split_by_tile            = COMPUTE_DEMUX_PARAMS.split_by_tile,
    )

    return (
        fastq_path          = MERGE_FASTQS_FROM_TILES.demultiplexed_fastq_path,
        demultiplex_summary = DEMULTIPLEX.demultiplex_summary,
        si_read_type        = COMPUTE_DEMUX_PARAMS.si_read_type,
    )
}
