#
# Copyright (c) 2015 10X Genomics, Inc. All rights reserved.
#
@include "chemistry_detector.mro"
@include "sc_rna_analyzer.mro"
@include "_common_stages.mro"
@include "_basic_sc_rna_counter.mro"
@include "_basic_sc_rna_counter_stages.mro"
@include "_sc_rna_counter_stages.mro"
@include "_crispr_analyzer.mro"

pipeline SC_RNA_COUNTER(
    in  string   sample_id,
    in  map[]    sample_def,
    in  string   chemistry_name,
    in  map      custom_chemistry_def,
    in  float    barcode_confidence_threshold,
    in  int      umi_min_qual_threshold,
    in  map      align,
    in  int      align_threads,
    in  path     reference_path,
    in  map[]    primers,
    in  int      recovered_cells,
    in  int      force_cells,
    in  float    subsample_rate,
    in  int      initial_reads,
    in  string   sample_desc,
    in  int      r1_length                      "Trim input R1 to this length (optional)",
    in  int      r2_length                      "Trim input R2 to this length (optional)",
    in  map      annotation_params,
    in  bool     no_secondary_analysis,
    in  bool     filter_umis,
    in  bool     retain_fastqs,
    in  string[] allowed_chems,
    in  string[] library_type_filter,
    in  csv      feature_reference,
    in  int      num_reads_to_check_barcode,
    in  float    barcode_compatibility_cutoff,
    out bam      possorted_genome_bam,
    out bam.bai  possorted_genome_bam_index,
    out json     metrics_summary_json,
    out csv      metrics_summary_csv,
    out html     web_summary,
    out h5       barcode_summary,
    out h5       molecule_info,
    out h5       raw_gene_bc_matrices_h5,
    out path     raw_gene_bc_matrices_mex,
    out h5       filtered_gene_bc_matrices_h5,
    out path     filtered_gene_bc_matrices_mex,
    out path     analysis,
    out path     analysis_csv,
    out path     crispr_analysis,
    out string   barcode_whitelist,
    out fastq[]  read_fastqs,
    out map[]    fastq_chunks,
    out map      chemistry_def,
    out map      align,
    out int[]    gem_groups,
    out csv      filtered_barcodes,
    out string   chemistry_type,
    out map[]    library_info,
)
{
    call CELLRANGER_PREFLIGHT(
        sample_def           = self.sample_def,
        chemistry_name       = self.chemistry_name,
        custom_chemistry_def = self.custom_chemistry_def,
        reference_path       = self.reference_path,
        feature_reference    = self.feature_reference,
        check_executables    = true,
        recovered_cells      = self.recovered_cells,
        force_cells          = self.force_cells,
        allowed_chems        = self.allowed_chems,
        r1_length            = self.r1_length,
        r2_length            = self.r2_length,
    ) using (
        preflight = true,
    )

    call CELLRANGER_PREFLIGHT_LOCAL(
        sample_def           = self.sample_def,
        chemistry_name       = self.chemistry_name,
        custom_chemistry_def = self.custom_chemistry_def,
        reference_path       = self.reference_path,
        feature_reference    = self.feature_reference,
        check_executables    = false,
        recovered_cells      = self.recovered_cells,
        force_cells          = self.force_cells,
        allowed_chems        = self.allowed_chems,
        r1_length            = self.r1_length,
        r2_length            = self.r2_length,
    ) using (
        local     = true,
        preflight = true,
    )

    call CHEMISTRY_DETECTOR(
        sample_id           = self.sample_id,
        sample_def          = self.sample_def,
        reference_path      = self.reference_path,
        vdj_reference_path  = null,
        chemistry_name_spec = self.chemistry_name,
        allowed_chems       = self.allowed_chems,
        r1_length           = self.r1_length,
        r2_length           = self.r2_length,
    )

    call DISABLE_FEATURE_STAGES(
        sample_def = self.sample_def,
    )

    call SETUP_CHUNKS(
        sample_id            = self.sample_id,
        sample_def           = self.sample_def,
        library_type_filter  = self.library_type_filter,
        chemistry_name       = CHEMISTRY_DETECTOR.chemistry_type,
        custom_chemistry_def = self.custom_chemistry_def,
        default_library_type = null,
    ) using (
        local    = true,
        volatile = true,
    )

    call CHECK_BARCODES_COMPATIBILITY(
        chunks                       = SETUP_CHUNKS.chunks,
        barcode_whitelist            = SETUP_CHUNKS.barcode_whitelist,
        num_reads_to_check_barcode   = self.num_reads_to_check_barcode,
        barcode_compatibility_cutoff = self.barcode_compatibility_cutoff,
    )

    call _BASIC_SC_RNA_COUNTER(
        sample_id                    = self.sample_id,
        chemistry_def                = SETUP_CHUNKS.chemistry_def,
        barcode_whitelist            = SETUP_CHUNKS.barcode_whitelist,
        barcode_confidence_threshold = self.barcode_confidence_threshold,
        umi_min_qual_threshold       = self.umi_min_qual_threshold,
        align                        = self.align,
        align_threads                = self.align_threads,
        reference_path               = self.reference_path,
        recovered_cells              = self.recovered_cells,
        force_cells                  = self.force_cells,
        cell_barcodes                = null,
        correct_barcodes             = true,
        rescue_multimappers          = true,
        skip_metrics                 = false,
        skip_translate               = CHECK_BARCODES_COMPATIBILITY.skip_translate,
        annotation_params            = self.annotation_params,
        filter_umis                  = self.filter_umis,
        retain_fastqs                = self.retain_fastqs,
        chunks                       = SETUP_CHUNKS.chunks,
        subsample_rate               = self.subsample_rate,
        initial_reads                = self.initial_reads,
        primers                      = self.primers,
        r1_length                    = self.r1_length,
        r2_length                    = self.r2_length,
        feature_reference            = self.feature_reference,
        library_info                 = SETUP_CHUNKS.library_info,
    )

    call SC_RNA_ANALYZER(
        raw_matrices_h5            = _BASIC_SC_RNA_COUNTER.raw_gene_bc_matrices_h5,
        filtered_matrices_h5       = _BASIC_SC_RNA_COUNTER.filtered_gene_bc_matrices_h5,
        no_secondary_analysis      = self.no_secondary_analysis,
        aggr_library_info          = null,
        num_analysis_bcs           = null,
        num_pca_bcs                = null,
        num_pca_genes              = null,
        num_principal_comps        = null,
        chemistry_batch_correction = false,
        cbc_knn                    = null,
        cbc_alpha                  = null,
        cbc_sigma                  = null,
        cbc_realign_panorama       = null,
        max_clusters               = null,
        graphclust_neighbors       = null,
        neighbor_a                 = null,
        neighbor_b                 = null,
        tsne_perplexity            = null,
        tsne_input_pcs             = null,
        random_seed                = null,
        tsne_theta                 = null,
        use_genes                  = null,
        exclude_genes              = null,
        use_bcs                    = null,
        tsne_max_dims              = null,
        tsne_max_iter              = null,
        tsne_stop_lying_iter       = null,
        tsne_mom_switch_iter       = null,
        # NOTE: this is null because the cells are already forced in FILTER_BARCODES
        force_cells                = null,
    )

    call _CRISPR_ANALYZER(
        molecule_info        = _BASIC_SC_RNA_COUNTER.molecule_info,
        filtered_feature_counts_matrix = _BASIC_SC_RNA_COUNTER.filtered_gene_bc_matrices_h5,
        filtered_barcodes    = _BASIC_SC_RNA_COUNTER.filtered_barcodes,
        feature_reference    = self.feature_reference,
        counter_metrics_json = _BASIC_SC_RNA_COUNTER.summary,
    ) using (
        disabled = DISABLE_FEATURE_STAGES.disable_crispr,
    )

    call SUMMARIZE_REPORTS(
        summaries                    = [
            _BASIC_SC_RNA_COUNTER.summary,
            SC_RNA_ANALYZER.summary,
            _CRISPR_ANALYZER.crispr_analysis_metrics,
        ],
        sample_id                    = self.sample_id,
        sample_desc                  = self.sample_desc,
        reference_path               = self.reference_path,
        analysis                     = SC_RNA_ANALYZER.analysis,
        barcode_summary_h5           = _BASIC_SC_RNA_COUNTER.barcode_summary,
        filtered_gene_bc_matrices_h5 = _BASIC_SC_RNA_COUNTER.filtered_gene_bc_matrices_h5,
        filtered_barcodes            = _BASIC_SC_RNA_COUNTER.filtered_barcodes,
        barcode_whitelist            = SETUP_CHUNKS.barcode_whitelist,
        gem_groups                   = _BASIC_SC_RNA_COUNTER.gem_groups,
    )

    return (
        possorted_genome_bam          = _BASIC_SC_RNA_COUNTER.possorted_genome_bam,
        possorted_genome_bam_index    = _BASIC_SC_RNA_COUNTER.possorted_genome_bam_index,
        web_summary                   = SUMMARIZE_REPORTS.web_summary,
        metrics_summary_json          = SUMMARIZE_REPORTS.metrics_summary_json,
        metrics_summary_csv           = SUMMARIZE_REPORTS.metrics_summary_csv,
        barcode_summary               = _BASIC_SC_RNA_COUNTER.barcode_summary,
        molecule_info                 = _BASIC_SC_RNA_COUNTER.molecule_info,
        raw_gene_bc_matrices_h5       = _BASIC_SC_RNA_COUNTER.raw_gene_bc_matrices_h5,
        raw_gene_bc_matrices_mex      = _BASIC_SC_RNA_COUNTER.raw_gene_bc_matrices_mex,
        filtered_gene_bc_matrices_h5  = _BASIC_SC_RNA_COUNTER.filtered_gene_bc_matrices_h5,
        filtered_gene_bc_matrices_mex = _BASIC_SC_RNA_COUNTER.filtered_gene_bc_matrices_mex,
        analysis                      = SC_RNA_ANALYZER.analysis,
        analysis_csv                  = SC_RNA_ANALYZER.analysis_csv,
        crispr_analysis               = _CRISPR_ANALYZER.crispr_analysis,
        barcode_whitelist             = SETUP_CHUNKS.barcode_whitelist,
        read_fastqs                   = _BASIC_SC_RNA_COUNTER.read1s,
        fastq_chunks                  = SETUP_CHUNKS.chunks,
        chemistry_def                 = SETUP_CHUNKS.chemistry_def,
        align                         = _BASIC_SC_RNA_COUNTER.align,
        gem_groups                    = _BASIC_SC_RNA_COUNTER.gem_groups,
        filtered_barcodes             = _BASIC_SC_RNA_COUNTER.filtered_barcodes,
        chemistry_type                = CHEMISTRY_DETECTOR.chemistry_type,
        library_info                  = SETUP_CHUNKS.library_info,
    )
}
