#
# Copyright (c) 2017 10X Genomics, Inc. All rights reserved.
#
@include "_common_stages.mro"
@include "_basic_sc_rna_counter_stages.mro"

pipeline _BASIC_SC_RNA_COUNTER(
    in  string  sample_id,
    in  map     chemistry_def,
    in  string  barcode_whitelist,
    in  map[]   chunks,
    in  float   barcode_confidence_threshold,
    in  int     umi_min_qual_threshold,
    in  map     align,
    in  int     align_threads,
    in  path    reference_path,
    in  int     recovered_cells,
    in  int     force_cells,
    in  json    cell_barcodes                  "Cell barcode override",
    in  bool    correct_barcodes,
    in  bool    rescue_multimappers,
    in  bool    skip_metrics,
    in  map     skip_translate,
    in  map     annotation_params,
    in  bool    filter_umis,
    in  bool    retain_fastqs,
    in  float   subsample_rate,
    in  int     initial_reads,
    in  map[]   primers,
    in  int     r1_length,
    in  int     r2_length,
    in  csv     feature_reference,
    in  map[]   library_info,
    out csv     filtered_barcodes,
    out bam     possorted_genome_bam,
    out bam.bai possorted_genome_bam_index,
    out json    summary,
    out h5      barcode_summary,
    out h5      molecule_info,
    out h5      raw_gene_bc_matrices_h5,
    out path    raw_gene_bc_matrices_mex,
    out h5      filtered_gene_bc_matrices_h5,
    out path    filtered_gene_bc_matrices_mex,
    out map     align,
    out int[]   gem_groups,
    out fastq[] read1s,
)
{
    call CHUNK_READS(
        chunks         = self.chunks,
        reads_per_file = 5000000,
    ) using (
        volatile = true,
    )

    call EXTRACT_READS(
        chunks            = CHUNK_READS.out_chunks,
        chemistry_def     = self.chemistry_def,
        barcode_whitelist = self.barcode_whitelist,
        reads_per_file    = 5000000,
        subsample_rate    = self.subsample_rate,
        initial_reads     = self.initial_reads,
        primers           = self.primers,
        align             = self.align,
        r1_length         = self.r1_length,
        r2_length         = self.r2_length,
        skip_metrics      = false,
        reference_path    = self.reference_path,
        feature_reference = self.feature_reference,
        augment_fastq     = false,
        library_info      = self.library_info,
    ) using (
        volatile = true,
    )

    call SUMMARIZE_READ_REPORTS(
        extract_reads_summary = EXTRACT_READS.summary,
        barcode_counts        = EXTRACT_READS.barcode_counts,
        feature_counts        = EXTRACT_READS.feature_counts,
        gem_groups            = EXTRACT_READS.gem_groups,
        library_types         = EXTRACT_READS.library_types,
        library_ids           = EXTRACT_READS.library_ids,
        read_groups           = EXTRACT_READS.read_groups,
        align                 = EXTRACT_READS.align,
        bam_comments          = EXTRACT_READS.bam_comments,
        read1s                = EXTRACT_READS.reads,
        read2s                = EXTRACT_READS.read2s,
        tags                  = EXTRACT_READS.tags,
        retain_fastqs         = self.retain_fastqs,
        chemistry_def         = self.chemistry_def,
    ) using (
        volatile = true,
    )

    call ALIGN_READS(
        reads             = EXTRACT_READS.reads,
        read2s            = EXTRACT_READS.read2s,
        read_groups       = SUMMARIZE_READ_REPORTS.read_groups,
        library_types     = SUMMARIZE_READ_REPORTS.library_types,
        reference_path    = self.reference_path,
        threads           = self.align_threads,
        max_hits_per_read = -1,
    ) using (
        volatile = true,
    )

    call ATTACH_BCS_AND_UMIS(
        genome_inputs                = ALIGN_READS.genome_output,
        tags                         = SUMMARIZE_READ_REPORTS.tags,
        reference_path               = self.reference_path,
        feature_reference            = self.feature_reference,
        gem_groups                   = SUMMARIZE_READ_REPORTS.gem_groups,
        library_types                = SUMMARIZE_READ_REPORTS.library_types,
        library_ids                  = SUMMARIZE_READ_REPORTS.library_ids,
        chemistry_def                = self.chemistry_def,
        annotation_params            = self.annotation_params,
        barcode_whitelist            = self.barcode_whitelist,
        barcode_counts               = SUMMARIZE_READ_REPORTS.barcode_counts,
        feature_counts               = SUMMARIZE_READ_REPORTS.feature_counts,
        barcode_confidence_threshold = self.barcode_confidence_threshold,
        umi_min_qual_threshold       = self.umi_min_qual_threshold,
        bam_comments                 = SUMMARIZE_READ_REPORTS.bam_comments,
        rescue_multimappers          = self.rescue_multimappers,
        skip_metrics                 = self.skip_metrics,
        skip_translate               = self.skip_translate,
        correct_barcodes             = self.correct_barcodes,
        library_info                 = self.library_info,
    ) using (
        volatile = true,
    )

    call BUCKET_BY_BC(
        nbases         = 2,
        inputs         = ATTACH_BCS_AND_UMIS.output,
        num_alignments = ATTACH_BCS_AND_UMIS.num_alignments,
    ) using (
        volatile = true,
    )

    call SORT_BY_BC(
        buckets = BUCKET_BY_BC.buckets,
    ) using (
        volatile = true,
    )

    call MARK_DUPLICATES(
        input          = SORT_BY_BC,
        reference_path = self.reference_path,
        filter_umis    = self.filter_umis,
        library_info   = self.library_info,
    ) using (
        volatile = true,
    )

    call SORT_BY_POS(
        inputs      = MARK_DUPLICATES.output,
        num_threads = 6,
        mem_gb      = 2,
    )

    call COUNT_GENES(
        sample_id         = self.sample_id,
        inputs            = MARK_DUPLICATES.output,
        reference_path    = self.reference_path,
        feature_reference = self.feature_reference,
        gem_groups        = SUMMARIZE_READ_REPORTS.gem_groups,
        align             = SUMMARIZE_READ_REPORTS.align,
        chemistry_def     = self.chemistry_def,
        barcode_whitelist = self.barcode_whitelist,
        barcodes_detected = ATTACH_BCS_AND_UMIS.barcodes_detected,
    )

    call FILTER_BARCODES(
        sample_id              = self.sample_id,
        matrices_h5            = COUNT_GENES.matrices_h5,
        barcode_correction_csv = MARK_DUPLICATES.barcode_summary,
        raw_fastq_summary      = SUMMARIZE_READ_REPORTS.summary,
        attach_bcs_summary     = ATTACH_BCS_AND_UMIS.summary,
        recovered_cells        = self.recovered_cells,
        force_cells            = self.force_cells,
        barcode_summary        = COUNT_GENES.barcode_summary,
        barcode_whitelist      = self.barcode_whitelist,
        gem_groups             = SUMMARIZE_READ_REPORTS.gem_groups,
        chemistry_def          = self.chemistry_def,
        cell_barcodes          = self.cell_barcodes,
    )

    call REPORT_MOLECULES(
        inputs                      = MARK_DUPLICATES.output,
        reference_path              = self.reference_path,
        feature_reference           = self.feature_reference,
        align                       = SUMMARIZE_READ_REPORTS.align,
        barcode_whitelist           = self.barcode_whitelist,
        extract_reads_summary       = SUMMARIZE_READ_REPORTS.summary,
        attach_bcs_and_umis_summary = ATTACH_BCS_AND_UMIS.summary,
        mark_duplicates_summary     = MARK_DUPLICATES.summary,
        recovered_cells             = self.recovered_cells,
        force_cells                 = self.force_cells,
        filtered_barcodes           = FILTER_BARCODES.filtered_barcodes,
    )

    call SUBSAMPLE_READS(
        molecule_info     = REPORT_MOLECULES.output,
        filtered_barcodes = FILTER_BARCODES.filtered_barcodes,
    ) using (
        volatile = true,
    )

    call SUMMARIZE_BASIC_REPORTS(
        reference_path               = self.reference_path,
        align                        = SUMMARIZE_READ_REPORTS.align,
        barcode_whitelist            = self.barcode_whitelist,
        gem_groups                   = SUMMARIZE_READ_REPORTS.gem_groups,
        extract_reads_summary        = SUMMARIZE_READ_REPORTS.summary,
        attach_bcs_and_umis_summary  = ATTACH_BCS_AND_UMIS.summary,
        mark_duplicates_summary      = MARK_DUPLICATES.summary,
        count_genes_reporter_summary = COUNT_GENES.reporter_summary,
        filter_barcodes_summary      = FILTER_BARCODES.summary,
        subsample_molecules_summary  = SUBSAMPLE_READS.summary,
        raw_gene_bc_matrices_h5      = COUNT_GENES.matrices_h5,
        filtered_gene_bc_matrices_h5 = FILTER_BARCODES.filtered_matrices_h5,
    )

    return (
        filtered_barcodes             = FILTER_BARCODES.filtered_barcodes,
        possorted_genome_bam          = SORT_BY_POS.output,
        possorted_genome_bam_index    = SORT_BY_POS.index,
        summary                       = SUMMARIZE_BASIC_REPORTS.summary,
        barcode_summary               = COUNT_GENES.barcode_summary,
        molecule_info                 = REPORT_MOLECULES.output,
        raw_gene_bc_matrices_h5       = COUNT_GENES.matrices_h5,
        raw_gene_bc_matrices_mex      = COUNT_GENES.matrices_mex,
        filtered_gene_bc_matrices_h5  = FILTER_BARCODES.filtered_matrices_h5,
        filtered_gene_bc_matrices_mex = FILTER_BARCODES.filtered_matrices_mex,
        align                         = SUMMARIZE_READ_REPORTS.align,
        gem_groups                    = SUMMARIZE_READ_REPORTS.gem_groups,
        read1s                        = SUMMARIZE_READ_REPORTS.read1s,
    )
}
